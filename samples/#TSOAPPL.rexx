/* REXX */

/* IMPORT THE API LIBRARY */
CALL IMPORT FSSAPI

/* ADDRESS THE FSS SUBSYSTEM */
ADDRESS FSS

/* SWITCH TO FULL SCREEN MODE */
CALL FSSINIT

/* CALL INTERNAL FUNCTION TO BUILD THE MENU SCREEN */
CALL MENU

/* SHOW SCREEN UNTIL PFK03 PRESSED */
DO FOREVER
   /* UPDATE FIELD VALUES */
   CALL UPDATE
   /* REFRESH / SHOW SCREEN */
   RCKEY=FSSREFRESH()
   IF RCKEY==#PFK03 | RCKEY==#PFK15 THEN LEAVE
   SEL = FSSFGET('_ZCMD')
   SELECT
      WHEN SEL == 1 THEN
        DO
          ADDRESS TSO
          "CALL 'SYS2.CMDLIB(RFE)'"
          ADDRESS FSS
        END
      WHEN SEL == 2 THEN
        DO
          ADDRESS TSO
          RPF
          ADDRESS FSS
        END
      WHEN SEL == 3 THEN
        DO
          ADDRESS TSO
          IM
          ADDRESS FSS
        END
      WHEN SEL == 4 THEN
        DO
          ADDRESS TSO
          Q
          ADDRESS FSS
        END
      WHEN SEL == 5 THEN
        DO
          ADDRESS TSO
          HELP
          ADDRESS FSS
        END
      WHEN SEL == 6 THEN
        DO
          ADDRESS TSO
          HELP UTILS
          ADDRESS FSS
        END
      WHEN SEL == '7' THEN
        DO
          ADDRESS TSO
          TERMTEST
          ADDRESS FSS
        END
      WHEN SEL == 'x' THEN LEAVE
      WHEN SEL == 'X' THEN LEAVE
    END
END

CALL FSSCLOSE
EXIT

MENU:
 CALL INFO1
 CALL INFO2
 CALL MSG
 CALL OPTION
 CALL HEADING1
 CALL HEADING2
 CALL LOGO
 CALL ENTRY 1,"RFE"      ,'"SPF like" productivity tool'
 CALL ENTRY 2,"RPF"      ,'"SPF like" productivity tool'
 CALL ENTRY 3,"IM"       ,'IMON/370 system monitor'
 CALL ENTRY 4,"QUEUE"    ,'spool browser'
 CALL ENTRY 5,"HELP"     ,'general TSO help'
 CALL ENTRY 6,"UTILS"    ,'information on utilities and commands available'
 CALL ENTRY 7,"TERMTEST" ,'verify 3270 terminal capabilities'
 CALL FOOTER
 CALL PFKEYS
RETURN

UPDATE:
 CALL FSSFSET   '_TERMID', "CUU0C2"; CALL FSSCOLOR '_TERMID',#RED 
 CALL FSSFSET   '_SYSID' , "TK4-";   CALL FSSCOLOR '_SYSID' ,#RED 
 CALL FSSFSET   '_USRID' , USERID()
 CALL FSSFSET   '_DATE'  , TODAY('SHGERMAN')
 CALL FSSFSET   '_TIME'  , TIME()
 CALL FSSCURSOR '_ZCMD'
RETURN

INFO1:
 CALL FSSTEXT "Terminal",1,1,,#PROT+#BLUE; CALL FSSFIELD '_TERMID',1,
              11,8,#PROT+#BLUE
 CALL FSSTEXT "System"  ,2,1,,#PROT+#BLUE; CALL FSSFIELD '_SYSID' ,2,
              11,8,#PROT+#BLUE
 CALL FSSTEXT "TSO User",3,1,,#PROT+#BLUE; CALL FSSFIELD '_USRID' ,3,
              11,8,#PROT+#BLUE
RETURN

INFO2:
 CALL FSSTEXT "Date",1,66,,#PROT+#BLUE; CALL FSSFIELD '_DATE',1,72,8,
              #PROT+#BLUE
 CALL FSSTEXT "Time",2,66,,#PROT+#BLUE; CALL FSSFIELD '_TIME',2,72,8,
              #PROT+#BLUE
RETURN

MSG:
 CALL FSSFIELD '_MSG'   ,4,1,40, #PROT+#HI+#WHITE, ''
RETURN

OPTION:
 CALL FSSTEXT "Option ===>",5,1,,#PROT+#GREEN; CALL FSSFIELD '_ZCMD',
              5,13,1,#TURQ
RETURN

HEADING1:
 CALL FSSTEXT "The MVS 3.8j Tur(n)key System",7,15,,#PROT+#YELLOW
 CALL FSSTEXT "TK4- Version 1.00 Update 08 -- MVS PUT 8505",8,8,,#PROT+#YELLOW
RETURN

HEADING2:
 CALL FSSTEXT "TSO Applications",10,22,,#PROT+#YELLOW+#REVERSE
RETURN

LOGO:
 CALL FSSTEXT "     ",7,59,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT " ",7,65,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT " ",7,68,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT "  ",7,71,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT "  ",7,71,,#PROT+#PINK+#REVERSE

 CALL FSSTEXT " ",8,59,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT " ",8,61,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT " ",8,63,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT " ",8,65,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT " ",8,67,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT " ",8,70,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT " ",8,72,,#PROT+#PINK+#REVERSE

 CALL FSSTEXT " ",9,61,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT "  ",9,65,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT "     ",9,69,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT "    ",9,75,,#PROT+#PINK+#REVERSE

 CALL FSSTEXT " ",10,61,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT " ",10,65,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT " ",10,67,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT " ",10,72,,#PROT+#PINK+#REVERSE

 CALL FSSTEXT "   ",11,60,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT " ",11,65,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT " ",11,68,,#PROT+#PINK+#REVERSE
 CALL FSSTEXT "   ",11,71,,#PROT+#PINK+#REVERSE
RETURN

ENTRY:
 PARSE ARG NUM,SHORT,LONG
 CALL FSSTEXT NUM  ,12+NUM, 6,,#PROT+#WHITE
 CALL FSSTEXT SHORT,12+NUM, 9,,#PROT+#TURQ
 CALL FSSTEXT LONG ,12+NUM,20,,#PROT+#GREEN
RETURN

FOOTER:
 CALL FSSTEXT "Enter"       ,21,20,,#PROT+#GREEN
 CALL FSSTEXT "X"           ,21,26,,#PROT+#TURQ
 CALL FSSTEXT "to Terminate",21,28,,#PROT+#GREEN
RETURN

PFKEYS:
 CALL FSSTEXT "PF3=Terminate",23,2,,#PROT+#BLUE
RETURN

         MACRO                                                          00010000
&NAME    VTCALLOC &DUMMY,&PERM,&DSN=,&DDN=,&DDNRET=,&MEMBER=,&DISP=,   X00020000
               &VOL=,&UNIT=,&SYSOUT=,&FREE=,&COPIES=,&LABEL=,          X00030000
               &BLKSIZE=,&DEN=,&DSORG=,&KEYLEN=,&LRECL=,&RECFM=,       X00040000
               &PASWORD=,&DSNRET=,&MF=AUTO,&PREFIX=,&ERROR=,           X00050000
               &SPACE=,&F=,&FILE=,&DA=,&QNAME=,&DSORGRT=,              X00060000
               &VOLRET=,&DCBDSN=,&DCBDDN=,&SPECIAL=,&DDNTO=,           X00070000
               &FORMS=,&DEST=,&SSREQ=,&FORUSER=,&TU=,&DSNPDE=           00080000
.*     MODIFICATIONS                                                    00081000
.*   IDENTIFIER   DATE   PERSON   REASON                                00082000
.*      001G     050284  GATES    RENAME ALLOC TO VTCALLOC,             00083001
.*                                 RENAME DYNSPACE TO VTCDYNSP TO MAKE  00083101
.*                                 VTOC MORE TRANSPORTABLE.             00084000
.*       CHANGED 12/15/80 JEFF SAVIT TO SUPPORT LONG RBS         801215 00090000
.********************************************************************** 00100000
.*                                                                    * 00110000
.*    THIS MACRO PROVIDES A DYNAMIC ALLOCATION FUNCTION BY BUILDING   * 00120000
.*    A DYNAMIC ALLOCATION PARAMETER LIST AND INVOKING SVC 99.        * 00130000
.*    IT FIRST SETS UP A WORKAREA ENVIRONMENT FOR THE PARAMETER LIST  * 00140000
.*    AND THEN TESTS THE KEYWORDS SUPPLIED AND INVOKES INNER MACROS   * 00150000
.*    TO BUILD THE TEXT UNITS. THE INNER MACROS THEMSELVES USE INNER  * 00160000
.*    MACROS TO UPDATE GLOBAL VARIABLES, STORE TEXT UNIT POINTERS ETC * 00170000
.*    THERE ARE THREE WAYS OF SPECIFYING THE WORK AREA ADDRESS.       * 00180000
.*    A) MF=AUTO, MF=G, MF=(E,ADDRESS,LNTHSYMB).                      * 00190000
.*    IN THE FIRST FORM, AN INNER MACRO VTCDYNSP IS CALLED TO NAME 001G 00200002
.*    A WORK AREA, THE NAME BEING RETURNED IN THE GLOBAL SETC         * 00210000
.*    VARIABLE &DYNSP. A DSECT IS CREATED TO MAP THIS AREA.           * 00220000
.*    THE GLOBAL VARIABLES &DTUO (TEXT UNIT OFFSET COUNTER) AND       * 00230000
.*    &DTUPO (TEXT UNIT POINTER OFFSET ACCUMULATOR) ARE SET TO ZERO.  * 00240000
.*    THESE ACCUMULATORS ARE UPDATED AS EACH TEXT UNIT PROCESSOR      * 00250000
.*    AQUIRES STORAGE. AFTER ALL TEXT UNITS HAVE BEEN BUILT, THE      * 00260000
.*    AMOUNT OF SPACE USED IS CALCULATED, AND THE VTCDYNSP MACRO IS001G 00270002
.*    THEN CALLED AGAIN TO LOG THE AMOUNT NEEDED. VTCDYNSP SETS A  001G 00280002
.*    GLOBAL VARIABLE &DYNSPQ TO THE HIGHEST AMOUNT ANY ALLOC OR      * 00290000
.*    FREE MACRO REQUESTED, AND WHEN CALLED WITH THE EXPAND OPTION,   * 00300000
.*    (NO OPERANDS OR NAME FIELD SUPPLIED), EXPANDS INTO A DS FOR     * 00310000
.*    THAT QUANTITY. (SEE VTCDYNSP)                                001G 00320002
.*    MF=G SPECIFIES THAT THE ALLOC MACRO ENTER THE BEGIN MACRO       * 00330000
.*    WORKAREA TO ACQUIRE THE STORAGE NECESSARY. IT DOES THIS VIA     * 00340000
.*    THE RCPDS MACRO. (SEE RCPDS). HOWEVER, IF THE ALLOC MACRO IS    * 00350000
.*    CALLED SEVERAL TIMES WITH THIS OPTION, A LOT OF STORAGE WILL BE * 00360000
.*    USED UP, AS THE STORAGE WILL NOT BE SHARED. THUS, THIS FORM     * 00370000
.*    SHOULD ONLY BE USED IF THE ALLOC/FREE MACRO IS ONLY TO BE USED  * 00380000
.*    ONCE OR TWICE DURING AN ASSEMBLY.                               * 00390000
.*    MF=E CAUSES THE MACRO TO USE A USER SPECIFIED WORK AREA. THE    * 00400000
.*    SECOND PARAMETER GIVES THE NAME OF THE WORKAREA, AND AN         * 00410000
.*    OPTIONAL THIRD PARAMETER IS THE NAME OF A SYMBOL TO BE EQUATED  * 00420000
.*    TO THE LENGTH OF THE REQUIRED WORK AREA.                        * 00430000
.*                                                                    * 00440000
.*    DYNAMIC ALLOCATION FUNCTIONS ARE SIMILAR TO THOSE AVAILABLE    *  00450000
.*    WITH JCL, USING THE SAME KEYWORDS. HOWEVER, CERTAIN FORMATS    *  00460000
.*    ARE SLIGHTLY DIFFERENT. FOR INSTANCE, CERTAIN KEYWORDS CAN     *  00470000
.*    HAVE VARYING PARAMETERS, EG DATASET NAME, DDNAME, VOLSER ETC.  *  00480000
.*    PROVISION IS MADE FOR BOTH VARIABLE SPECIFICATION.             *  00490000
.*    IN THE ABSOLUTE FORM, THE PARAMETER IS ENTERED IN QUOTES,      *  00500000
.*    E.G.   ALLOC DSN='SYS1.LINKLIB',DISP=SHR                       *  00510000
.*    HOWEVER, THIS NAME REMAINS FIXED FOR THE ASSEMBLY.             *  00520000
.*    IN THE VARIABLE FORMAT, THE ADDRESS OF A LOCATOR IS SPECIFIED, *  00530000
.*    WHERE THE LOCATOR CONSISTS OF A SIX BYTE FIELD, THE FIRST 4    *  00540000
.*    BYTES OF WHICH POINT TO THE PARAMETER, WHILE THE NEXT TWO      *  00550000
.*    CONTAIN THE LENGTH.                                            *  00560000
.*    EG          ALLOC DSN=LOCATOR                                  *  00570000
.*       LOCATOR  DC    A(DSN),Y(12)                                 *  00580000
.*       DSN      DC    C'SYS1.LINKLIB'                              *  00590000
.*                                                                   *  00600000
.*       NUMERIC QUANTITIES E.G. COPIES= FOR SYSOUT, SHOULD EITHER   *  00610000
.*       SPECIFY A NUMERIC VALUE, COPIES=3,                          *  00620000
.*       A VALUE IN A REGISTER, COPIES=(R3),                         *  00630000
.*       OR THE NAME OFF A FULLWORD CONTAINING THE VALUE,            *  00640000
.*          COPIES=NUMCOPYS, WHERE NUMCOPYS IS THE NAME OF A         *  00650000
.*       FULLWORD FIELD.                                             *  00660000
.*                                                                   *  00670000
.*       OTHER KEYWORDS SUCH AS DISP= CAN ONLY HAVE THE ABSOLUTE     *  00680000
.*       FORM, AND VALUES SHOULD NOT BE ENTERED WITHIN QUOTES.       *  00690000
.*       ADDITIONAL FACILITIES NOT AVAILABLE WITH JCL ARE THE        *  00700000
.*       RETURN BY THE SYSTEM OF INFORMATION ON THE DATASET, EG      *  00710000
.*       DSORG. THIS IS DONE BY SPECIFYING DSORGRT=SYMBOL, WHERE     *  00720000
.*       SYMBOL IS A SYMBOL WHICH WILL BE EQUATED TO A TWO BYTE      *  00730000
.*       FIELD CONTAINING THE DSORG TYPE (SEE JOB MANAGEMENT,        *  00740000
.*       SUPERVISOR AND TSO).                                        *  00750000
.*       THE SYSTEM CAN ALSO GENERATE AND RETURN A DDNAME. THIS IS   *  00760000
.*       CARRIED OUT BY ENTERING DDNTO=(ADDR1,ADDR2,,...)            *  00770000
.*       WHERE ADDR1,ADDR2 ETC ARE THE NAMES OF 8 BYTE FIELDS WHICH  *  00780000
.*       ARE TO RECEIVE THE DDNAME.                                  *  00790000
.*       FOR FURTHER INFORMATION ON DYNAMIC ALLOCATION, SEE          *  00800000
.*       JOB MANAGEMENT, SUPERVISOR AND TSO.                         *  00810000
.*                                                                   *  00820000
.********************************************************************** 00830000
         GBLA  &RCPDYN            COUNTER FOR NO ENTRIES TO MACRO       00840000
         GBLA  &DTUO              OFFSET TO TEXT UNITS                  00850000
         GBLA  &DTUPO             OFFSET TO TEXT UNIT POINTERS          00860000
         GBLB  &RCPS99(2)         TELL RCPDSECT NEED DSECTS             00870000
         GBLC  &DYNP              PREFIX FOR LABELS FOR THIS CALL       00880000
         GBLC  &DYNSP         NAME FOR AUTOMATIC STORAGE ALLOC          00890000
         LCLA  &DDNRTO,&DSNRTO         FOR EQUATES FOR RETURNED FLDS    00900000
         LCLA  &VOLRTO,&DSRGRTO        FOR EQUATES FOR RETURNED FIELDS  00910000
         LCLA  &I                 COUNTER                               00920000
         LCLB  &DSECT             DSECT NEEDED FOR STORAGE, MF=E        00930000
         LCLC  &C,&T,&PAR                                               00940000
.*                                                                      00950000
&RCPS99(1)     SETB           1                                         00960000
&RCPDYN  SETA  &RCPDYN+1          INCEREMENT COUNTER                    00970000
&DYNP    SETC  'DYN&RCPDYN' SET DEFAULT PREFIX                          00980000
&NAME    DS    0H                                                       00990000
         AIF   ('&PREFIX' EQ '').TMF                                    01000000
         AIF   (K'&PREFIX LT 4).POK                                     01010000
         MNOTE 4,'PREFIX TOO LONG, 1ST 4 CHARS USED'                    01020000
&DYNP    SETC  '&PREFIX'(1,4)                                           01030000
         AGO   .TMF                                                     01040000
.POK     ANOP                                                           01050000
&DYNP    SETC  '&PREFIX'                                                01060000
.TMF     AIF   ('&MF(1)' EQ 'G').GEN                                    01070000
         AIF   ('&MF' NE 'AUTO').TMFE                                   01080000
NAME     VTCDYNSP             GET NAME FOR SPACE                   001G 01090001
         LA    R1,&DYNSP               LOAD ADDRESS OF PARAM LIST       01100000
         USING &DYNP.DS,R1             USE GENERATED DSECT              01110000
&T       SETC  'A'                                                      01120000
&PAR     SETC  '&DYNSP+4'                                               01130000
&DSECT   SETB  1                                                        01140000
         AGO   .START                                                   01150000
.TMFE    AIF   ('&MF(2)' NE '').E2OK                                    01160000
         MNOTE 4,'PLIST ADDRESS OMITTED, MF=G USED'                     01170000
         AGO   .GEN                                                     01180000
.E2OK    ANOP                                                           01190000
&DSECT   SETB  1                                                        01200000
         AIF   ('&MF(2)' EQ '(').RMFE                                   01210000
         LA    R1,&MF(2)               LOAD PARAM LIST ADDRESS          01220000
         USING &DYNP.DS,R1             USE GENERATED DSECT              01230000
         AGO   .START                                                   01240000
.RMFE    AIF   ('&MF(2)' EQ '(R1)' OR '&MF(2)' EQ '(1)').START          01250000
         LR    R1,&PAR                 LOAD S99 PARAM LIST ADDRESS      01260000
         AGO   .START                                                   01270000
.GEN     LA    R1,&DYNP.RBP            LOAD ADDRESS OF S99 RBP          01280000
.START   LA    R15,&DYNP.RB            LOAD ADDRESS OF S99 RB           01290000
         USING S99RB,R15                                                01300000
         ST    R15,0(R1)               AND STORE IN RB POINTER          01310000
.* FOLLOWING LINE REPLACED, SINCE FAILS FOR LONG ALLOC REQUESTS  801215 01320000
.*       XC    4(&DYNP.LEN-4,R1),4(R1) ZERO PARAMETER LIST       801215 01330000
         XC    &DYNP.RB(20),&DYNP.RB   ZERO REQUEST BLOCK        801215 01340000
         XC    &DYNP.TUP,&DYNP.TUP     ZERO TEXT UNIT POINTERS   801215 01350000
         XC    &DYNP.TU,&DYNP.TU       ZERO TEXT UNITS           801215 01360000
.*                                                               801215 01370000
         MVI   S99RBLN,20              MOVE IN LIST LENGTH              01380000
         MVI   S99VERB,S99VRBAL        MOVE IN VERB CODE                01390000
         LA    R14,&DYNP.TUP           LOAD ADDRESS OF TU POINTERS      01400000
         ST    R14,S99TXTPP            STORE ADDRESS IN S99 RB          01410000
         LA    R15,&DYNP.TU            POINT TO SPACE FOR TEXT UNITS    01420000
         USING S99TUNIT,R15                                             01430000
&DTUO    SETA  0                                                        01440000
&DTUPO   SETA  0                                                        01450000
         AIF   ('&SSREQ' EQ 'YES').SSREQ                                01460000
.TDSN    AIF   ('&DSN&DA' NE '').DSN                                    01470000
         AIF   ('&DSNPDE' NE '').DSNPDE                                 01480000
         AIF   ('&DSNRET' NE '').DSNRT                                  01490000
         AIF   ('&SYSOUT' NE '').SYSOUT                                 01500000
         AIF   ('&DUMMY' NE '').DUMMY                                   01510000
         AIF   ('&QNAME' NE '').QNAME                                   01520000
.TDDN    AIF   ('&DDN&FILE&F' NE '').DDN                                01530000
         AIF   ('&DDNRET&DDNTO' NE '').DDNRT                            01540000
.TUNIT   AIF   ('&UNIT&VOL' NE '').UNIT                                 01550000
.TVOLRET AIF   ('&VOLRET' NE '').VOLRET                                 01560000
.TDSRGO  AIF   ('&DSORGRT' NE '').DSORGRT                               01570000
.TLABEL  AIF   ('&LABEL' NE '').LABEL                                   01580000
.TPSWD   AIF   ('&PASWORD' NE '').PASWORD                               01590000
.TFORUSE AIF   ('&FORUSER' NE '').FORUSER                               01600000
.TTU     AIF   ('&TU' NE '').TU                                         01610000
.TDISP   AIF   ('&DISP' NE '').DISP                                     01620000
.TSPACE  AIF   ('&SPACE' NE '').SPACE                                   01630000
.TLRECL  AIF   ('&LRECL' NE '').DCB                                     01640000
         AIF   ('&DEN' NE '').DCB                                       01650000
         AIF   ('&RECFM' NE '').DCB                                     01660000
         AIF   ('&BLKSIZE' NE '').DCB                                   01670000
         AIF   ('&DSORG' NE '').DCB                                     01680000
         AIF   ('&KEYLEN' NE '').DCB                                    01690000
.TDCBDSN AIF   ('&DCBDSN' NE '').DCBDSN                                 01700000
.TDCBDDN AIF   ('&DCBDDN' NE '').DCBDDN                                 01710000
.TFREE   AIF   ('&FREE' EQ 'CLOSE').FREE                         TE7343 01720000
.TPERM   AIF   ('&PERM' EQ 'PERM' OR '&PERM' EQ 'PERMANENT').PERM       01730000
         AIF   ('&DUMMY' EQ 'PERM' OR '&DUMMY' EQ 'PERMANENT').PERM     01740000
.TSPECI  AIF   ('&SPECIAL' NE '').SPECIAL                               01750000
         AGO   .SVC99                                                   01760000
.SSREQ   RCPSSREQ                                                       01770000
         AGO   .TDSN                                                    01780000
.DSN     RCPDSN &DSN&DA,&MEMBER                                         01790000
         AGO   .TDDN                                                    01800000
.DSNPDE  RCPDSNPD &DSNPDE                                               01810000
         AGO   .TDDN                                                    01820000
.DSNRT   RCPDSNRT &DSNRET                                               01830000
&DSNRTO  SETA  &DTUO-46                                                 01840000
         AGO   .TDDN                                                    01850000
.SYSOUT  RCPSYSOU &SYSOUT,COPIES=&COPIES,FREE=&FREE,DEST=&DEST,        X01860000
               FORMS=&FORMS                                             01870000
         AGO   .TDDN                                                    01880000
.DUMMY   RCPDUMMY &DUMMY                                                01890000
         AGO   .TDDN                                                    01900000
.QNAME   RCPQNAME &QNAME                                                01910000
         AGO   .TDDN                                                    01920000
.DDN     RCPDDN &DDN&F&FILE                                             01930000
         AGO   .TUNIT                                                   01940000
.DDNRT   RCPDDNRT &DDNRET                                               01950000
&DDNRTO  SETA  &DTUO-10                                                 01960000
         AGO   .TUNIT                                                   01970000
.UNIT   RCPUNIT &UNIT,&VOL                                              01980000
         AGO   .TVOLRET                                                 01990000
.VOLRET  RCPVOLRT &VOLRET                                               02000000
&VOLRTO  SETA  &DTUO-8                                                  02010000
         AGO   .TDSRGO                                                  02020000
.DSORGRT RCPDSRGR                                                       02030000
&DSRGRTO SETA  &DTUO-2                                                  02040000
         AGO   .TLABEL                                                  02050000
.LABEL   RCPLABEL &LABEL                                                02060000
         AGO   .TPSWD                                                   02070000
.PASWORD RCPPSWD &PASWORD                                               02080000
         AGO   .TFORUSE                                                 02090000
.FORUSER RCPFORUS &FORUSER                                              02100000
         AGO   .TTU                                                     02110000
.TU      RCPTU &TU                                                      02120000
         AGO   .TDISP                                                   02130000
.DISP    RCPDISP &DISP                                                  02140000
         AGO   .TSPACE                                                  02150000
.SPACE   RCPSPACE &SPACE                                                02160000
         AGO   .TLRECL                                                  02170000
.DCB     RCPDDCB LRECL=&LRECL,DEN=&DEN,RECFM=&RECFM,BLKSIZE=&BLKSIZE,  X02180000
               DSORG=&DSORG,KEYLEN=&KEYLEN                              02190000
         AGO .TDCBDSN                                                   02200000
.DCBDSN  RCPDCBDS &DCBDSN                                               02210000
         AGO .TDCBDDN                                                   02220000
.DCBDDN  RCPDCBDD &DCBDDN                                               02230000
         AGO .TFREE                                              TE7343 02240000
.FREE    RCPFREE  &FREE                                          TE7343 02250000
         AGO   .TPERM                                                   02260000
.PERM    RCPPERM                                                        02270000
         AGO   .TSPECI                                                  02280000
.SPECIAL RCPSPEC &SPECIAL                                               02290000
.SVC99   ANOP                                                           02300000
&DTUPO   SETA  &DTUPO-4                                                 02310000
         SPACE                                                          02320000
         MVI   &DYNP.TUP+&DTUPO,X'80'  SET HIGH ORDER BIT ON TEXT PTRS  02330000
         MVI   &DYNP.RBP,X'80'         SET HIGH ORDER BIT ON RB PTR     02340000
         RCPSR2 UNSAVE                                                  02350000
&DTUPO   SETA  &DTUPO+4                                                 02360000
         AIF   (NOT &DSECT).DYNA                                        02370000
         DROP  R1,R15                  DEACTIVATE ADDRESSABILITY        02380000
         LA    R14,4(R1)               POINT TO REQUEST BLOCK           02390000
.DYNA    DYNALLOC                                                       02400000
         AIF   (NOT &DSECT).LTR                                         02410000
         USING &DYNP.RB,R14            SET UP ADDRESSABILITY            02420000
**       NOTE  R14 HAS RB ADDRESS, R15 HAS SVC 99 RETURN CODE        ** 02430000
.LTR     AIF   ('&ERROR' EQ '').TDDTO                                   02440000
         LTR   R15,R15                 TEST RETURN CODE                 02450000
         BNZ   &ERROR                  BRANCH IF NON ZERO               02460000
.TDDTO   AIF   ('&DDNTO' EQ '').RESERVE                                 02470000
&I       SETA  0                                                        02480000
.DDNTOL  ANOP                                                           02490000
&I       SETA  &I+1                                                     02500000
         AIF   ('&DDNTO(&I)' EQ '').RESERVE                             02510000
         AIF   ('&DDNTO(&I)'(1,1) EQ '(').DDNTOR                        02520000
         MVC   &DDNTO(&I).(8),&DYNP.TU+&DDNRTO+2                        02530000
         AGO   .DDNTOL                                                  02540000
.DDNTOR  ANOP                                                           02550000
&C       SETC  '&DDNTO(&I)'(2,K'&DDNTO(&I)-2)                           02560000
         MVC   0(8,&C),&DYNP.TU+&DDNRTO+2                               02570000
         AGO   .DDNTOL                                                  02580000
.RESERVE AIF   (&DSECT).RESDS                                           02590000
         SPACE 1                                                        02600000
*********************************************************************** 02610000
**       RESERVE SPACE FOR DYNALLOC PARAMETER LIST                   ** 02620000
*********************************************************************** 02630000
         RCPDS                                                          02640000
.SSP     ANOP                                                           02650000
&DYNP.RBP DS   F                       SVC 99 REQ BLOCK POINTER         02660000
&DYNP.RB  DS   5F                      SVC 99 REQUEST BLOCK             02670000
&DYNP.TUP DS   CL&DTUPO                SPACE FOR TEXT POINTERS          02680000
         AIF   (&DTUO EQ 0).DTU21                                       02690000
&DYNP.TU  DS   CL&DTUO                 SPACE FOR TEXT UNITS             02700000
         AIF   (&DSNRTO EQ 0).TDDNRTO                                   02710000
&DSNRET  EQU   &DYNP.TU+&DSNRTO        OFFSET TO RETURNED DSN           02720000
.TDDNRTO AIF   ('&DDNRET' EQ '').DTU11                                  02730000
&DDNRET  EQU   &DYNP.TU+&DDNRTO        OFFSET TO RETURNED DDNAME        02740000
.DTU11   AIF   (&VOLRTO EQ 0).DTU12                                     02750000
&VOLRET  EQU   &DYNP.TU+&VOLRTO        OFFSET TO RETURNED VOLSER        02760000
.DTU12   AIF   (&DSRGRTO EQ 0).DTU10                                    02770000
&DSORGRT EQU   &DYNP.TU+&DSRGRTO       OFFSET TO RETURNED DSORG         02780000
         AGO   .DTU10                                                   02790000
.DTU21   ANOP                                                           02800000
&DYNP.TU  DS   0C                      NO SPACE NEEDED FOR TEXT UNITS   02810000
.DTU10   ANOP                                                           02820000
&DYNP.LEN EQU  *-&DYNP.RBP             LENGTH OF SPACE USED             02830000
         AIF   (&DSECT).DSP                                             02840000
         RCPDS                                                          02850000
         SPACE 3                                                        02860000
         AGO   .EXIT                                                    02870000
.RESDS   ANOP                                                           02880000
         AIF   ('&DYNSP' EQ '').SP3                                     02890000
         VTCDYNSP ADD                                              001G 02900001
.SP3     SPACE                                                          02910000
&DYNP.DS DSECT                         DSECT TO MAP SVC 99 DATA         02920000
         AGO   .SSP                                                     02930000
.DSP     AIF   ('&MF(3)' EQ '').END1                                    02940000
&MF(3)   EQU   &DYNP.LEN               LENGTH OF AREA                   02950000
.END1    ANOP                                                           02960000
&SYSECT  CSECT                                                          02970000
         SPACE 3                                                        02980000
.EXIT    MEND                                                           02990000

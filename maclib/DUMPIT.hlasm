         MACRO
&LABEL   DUMPIT &ADR,&LEN,&CMT,&MODE=CALL,&TYPE=ADDR,&LEVEL=-1
         LCLA  &CLEN
         LCLA  &FLEN
         LCLC  &ADRVAR
         LCLC  &DATLN
         AIF   ('&MODE' EQ 'DEFINE').DEFINE
.* ------------------------------------------------------------
.*   SET TYPE: ADDRESS OR DISPLACEMENT MODE
.* ------------------------------------------------------------
         AIF   ('&TYPE'(1,3) EQ 'DIS').DISP
         LA    R1,0
         AGO   .TYPE
.DISP    ANOP
         LA    R1,1
.TYPE    ANOP
         ST    R1,DUMPDIS
.* ------------------------------------------------------------
.*   BUILD CALL OF DUMPIT
.* ------------------------------------------------------------
.* ..... SET LENGTH OF AREA AND COMMENT LENGTH (IF ANY)
&CLEN    SETA  0
         AIF   ('&CMT' EQ '').NOCMT
&CLEN    SETA  K'&CMT-2
.NOCMT   ANOP
&DATLN   SETC  '128'
         AIF   ('&LEN' EQ '').NOLEN
&DATLN   SETC  '&LEN'
.NOLEN   ANOP
&LABEL   DS    0H
         AIF   ('&ADR'(2,4) EQ 'REGS').REGS
         AIF   ('&ADR'(2,4) EQ 'PARM').PARM
         AIF   ('&ADR'(2,4) EQ 'SAVE').SAVE
         AGO   .ADDR
.* ------------------------------------------------------------
.*   BUILD CALL FOR REGISTER DUMP
.* ------------------------------------------------------------
.REGS    ANOP
         STM   R0,RF,DUMPREG           SAVE ALL REGISTERS
         LA    R1,DUMPREG              LOAD ADDRESSS OF REG AREA
         ST    R1,DUMPADR              STORE IT
         LA    R1,1                    MODE 1: REGISTER DUMP
         ST    R1,DUMPMOD
         LA    R1,64                   FIXED LENGTH IS 2 LINES
         ST    R1,DUMPLEN              SAVE IT
         AGO   .DMPREM
.* ------------------------------------------------------------
.*   BUILD CALL FOR PARM DUMP
.* ------------------------------------------------------------
.PARM    ANOP
         L     R1,4(RD)                POINT TO CALLER'S REGSAVE
         L     R1,24(R1)               LOAD ADDRESS OF CALLERS R1
         ST    R1,DUMPADR              STORE IT
         LA    R1,2                    MODE 1: REGISTER DUMP
         ST    R1,DUMPMOD
         AIF   ('&LEN' NE '').PHASLEN
&DATLN   SETC  '64'
.PHASLEN ANOP
         LA    R1,&DATLN               FIXED LENGTH IS 2 LINES
         ST    R1,DUMPLEN              SAVE IT
         AGO   .DMPREM
.* ------------------------------------------------------------
.*   BUILD CALL FOR CALLERS SAVE AREA DUMP
.* ------------------------------------------------------------
.SAVE    ANOP
         LR    R1,RD                   POINT TO CURRENT REGSAVE
         LA    RF,-&LEVEL              LOAD REQUESTED LEVEL
         STH   RF,DUMPLVL              SAVE LEVEL
         LTR   RF,RF                   IF 0, DISPLAY CURRENT SA
         BZ    S&SYSNDX
L&SYSNDX L     R1,4(R1)                POINT TO CALLER'S REGSAVE
         LTR   R1,R1                   IS IT SET
         BZ    S&SYSNDX                NO, BREAK OUT
         BCT   RF,L&SYSNDX             FURTHER LEVEL REQUESTED
S&SYSNDX ST    R1,DUMPADR              STORE IT
         LA    R1,3                    MODE 3: SAVE AREA DUMP
         STH   R1,DUMPLMD              SAVE DUMP MODE (SHORT)
         LA    R1,72                   FIXED LENGTH IS 2 LINES
         ST    R1,DUMPLEN              SAVE IT
         AGO   .DMPREM
.* ------------------------------------------------------------
.*   BUILD CALL FOR AN AREA
.* ------------------------------------------------------------
.ADDR    ANOP
         AIF   ('&ADR'(1,1) EQ '(').ISREG
         AIF   ('&ADR'(1,1) EQ '*').ISADDR
         LA    R1,&ADR
         AIF   ('&LEN' NE '').ADR2
&DATLN   SETC  'L''&ADR'
         AGO   .ADR2
.* ..... ADDRESS IS IN A FIELD
.ISADDR  ANOP
&FLEN    SETA  K'&ADR-1
&ADRVAR  SETC  '&ADR'(2,&FLEN)
         L     R1,&ADRVAR
         AGO   .ADR2
.* ..... ADDRESS IS IN REGISTER NOTATION
.ISREG   ANOP
         LR    R1,&ADR(1)
.* ..... SAVE FETCHED ADDRESS
.ADR2    ANOP
         ST    R1,DUMPADR
.* ..... SAVE EXTRACTED AREA LENGTH
         LA    R1,&DATLN
         ST    R1,DUMPLEN
.* ..... CALCULATE COMMENT LENGTH (IF ANY)
.DMPREM  AIF   ('&CLEN' NE '0').NOCMT2
         LA    R1,0
         AGO   .DMPCMT
.NOCMT2  ANOP
.* ..... FETCH COMMENT
         LA    R1,=C&CMT
.DMPCMT  ANOP
.* ..... SAVE COMMENT AND ITS LENGTH
         ST    R1,DUMPCMT
         LA    R1,&CLEN
         ST    R1,DUMPCLN
.* ------------------------------------------------------------
.*   PREPARE LINK OF DUMP MODULE
.* ------------------------------------------------------------
         LA    R1,DUMPPARM
         LINK  EP=MVSDUMP
         MEXIT
.* ------------------------------------------------------------
.*   BUILD DEFINITION OF DUMPIT PARMS, MAYBE USED WITHIN DSECT
.* ------------------------------------------------------------
.DEFINE  ANOP
DUMPPARM DS    0A
DUMPADR  DS    A
DUMPLEN  DS    A
DUMPMOD  DS    0A
DUMPLVL  DS    H
DUMPLMD  DS    H
DUMPCMT  DS    A
DUMPCLN  DS    A
DUMPDIS  DS    A          DISPLACEMENT MODE
DUMPREG  DS    16A
         MEND

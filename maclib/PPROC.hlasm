.* -------------------------------------------------------------------- 00000100
.*       RPROC MACRO, START MAIN PROCEDURE                              00000200
.* -------------------------------------------------------------------- 00000300
         MACRO                                                          00000400
&PGM     PPROC &TITLE=,&WORK=WORKAREA,&WORKLEN=WORKLEN,&WORKREG=RB,    X00000500
               &PGMREG=RC,&PGMTYPE=MAIN                                 00000600
.*             &AMODE=31,&RMODE=ANY                                     00000700
* --------------------------------------------------------------------- 00000800
         MNOTE '*     PROGRAM &PGM'                                     00000900
* --------------------------------------------------------------------- 00001000
         AIF   ('&PGMTYPE' EQ 'MAIN').NLTORG                            00001103
         LTORG                                                          00001203
.NLTORG  ANOP                                                           00001303
&PGM     CSECT                                                          00001400
         GBLC  &MAIN,&PTYPE                                             00001504
         GBLC  &PGM1,&PGM2,&WRK1,&WRK2                                  00001600
&MAIN    SETC  '&PGM'                                                   00001700
&PTYPE   SETC  '&PGMTYPE'                                               00001804
&PGM1    SETC  '&PGMREG(1)'                                             00001900
&WRK1    SETC  '&WORKREG(1)'                                            00002000
*. &PGM     AMODE &AMODE                                                00002100
*. &PGM     RMODE &RMODE                                                00002200
         USING &PGM,RF              AND SET ADDRESSABILITY              00002300
         B     B&SYSNDX             BRANCH TO ENTRY                     00002400
         DC    CL8'&MAIN'           PROGRAM NAME                        00002500
         DC    CL8'&SYSDATE',CL8'&SYSTIME'  DATE/TIME OF ASSEMBLY       00002600
         DC    CL72&TITLE                                               00002700
* --------------------------------------------------------------------- 00002800
*     ALLOCATE AND CHAIN SAVE AREA FOR HOUSEKEEPING                     00002900
* --------------------------------------------------------------------- 00003000
B&SYSNDX STM   RE,RC,12(RD)           SAVE CALLER'S REGS, RD->SAVEAREA  00003100
         DROP  RF                     RELEASE BASE REGISTER             00003200
         AIF   ('&PGMREG'(1,1) NE '(').JUSTONE                          00003300
         LR    &PGMREG(1),RF          GET FIRST BASE ADDRESS            00003400
         LA    &PGMREG(2),2048(&PGMREG(1))  SET SECOND BASE REGISTER    00003500
         LA    &PGMREG(2),2048(&PGMREG(2))  SET SECOND BASE REGISTER    00003600
* -------------------------------------------------                     00003700
*     PROGRAM BASE REGISTERS ARE &PGMREG(1),&PGMREG(2)                  00003804
* -------------------------------------------------                     00003900
&PGM2    SETC  '&PGMREG(2)'                                             00004000
         USING &PGM,&PGMREG(1),&PGMREG(2)   SET ADDRESSABILTY           00004100
         AGO   .BASESET                                                 00004200
.JUSTONE ANOP                                                           00004300
         LR    &PGMREG,RF             GET FIRST BASE ADDRESS            00004401
         USING &PGM,&PGMREG           AND SET ADDRESSABILITY            00004500
* -------------------------------------------------                     00004600
*     PROGRAM BASE REGISTERS IS &PGMREG(1)                              00004700
* -------------------------------------------------                     00004800
.BASESET ANOP                                                           00004900
         LR    R9,R1        *****     SAVE PARM ADDRESS                 00005006
.*       STORAGE OBTAIN,LOC=ANY,LENGTH=200 SAVE AREA&MORE               00005104
         GETMAIN RC,LV=256,SP=47                                        00005204
         ST    R1,0(R1)               IDENTIFY SAVE AREA BY ITS ADDRESS 00005300
         MVC   4(4,R1),=A(256)        ... AND ITS LENGTH                00005400
         MVC   8(20,R1),=CL20'*** SAVE-AREA ***'  SET EYECATCHER        00005504
         MVC   28(8,R1),=CL8'&PGM'                                      00005604
         LA    RE,36(R1)              NEW SAVE AREA, START CHAINING     00005704
         ST    RD,4(RE)               BACKWARD CHAIN TO CALLER IN NEW   00005800
         ST    RE,8(RD)               FORWARD  CHAIN FROM CALLER (OLD)  00005900
         LR    RD,RE                  POINT TO NEW SAVE AREA            00006000
         USING PGMSAVE,RD             ADDRESSABILITY SAVE AREA          00006100
         ST    R1,PGMBGPTR            FULL SAVE AREA BEGIN PTR          00006200
         L     R1,4(RD)               POINT TO CALLER'S REGSAVE         00006300
         L     R0,20(R1)              LOAD ADDRESS IRXENV CB            00006404
         L     R1,24(R1)              LOAD ADDRESS OF JCLPARM           00006504
         AIF   ('&PGMTYPE' NE 'MAIN').JCL2                              00006600
         L     R1,0(R1)               POINT TO JCLPARM                  00006700
.JCL2    ANOP                                                           00006800
         ST    R0,IRXENV              SAVE R0 FOR REXX ENVCB, IF ANY    00006904
         ST    R1,JCLPARM             SAVE POINTER IN PARM SAVE         00007004
         AIF   ('&PGMTYPE' NE 'MAIN').NOWA                              00007104
* --------------------------------------------------------------------- 00007200
*     DEFINE WORK AREA                                                  00007300
* --------------------------------------------------------------------- 00007400
         GETMAIN RC,LV=&WORKLEN+36,SP=47 GET WORK AREA                  00007504
         ST    R1,PGMWAPTR            FULL SAVE AREA BEGIN PTR          00007600
         ST    R1,0(R1)               IDENTIFY WA BY ITS ADDRESS        00007700
*        CLEARMEM ADDR=(R1),LEN=&WORKLEN+36,PAD='00'                    00007802
         L     R1,PGMWAPTR            FULL SAVE AREA BEGIN PTR          00007901
         MVC   4(4,R1),=A(&WORKLEN+36)  ... AND ITS LENGTH              00008000
         MVC   8(20,R1),=CL20'*** WORKAREA ***'  SET EYECATCHER         00008100
         MVC   28(8,R1),=CL8'&PGM'                                      00008200
         LA    &WORKREG,36(R1)        LOAD ADDRESS OF 1. BYTE WA        00008300
* -------------------------------------------------                     00008400
*     DATA/WORK ARE BASE REGISTERS IS &WRK1                             00008500
* -------------------------------------------------                     00008600
         USING &WORK,&WORKREG         AND SET ADDRESSABILITY            00008700
* --------------------------------------------------------------------- 00008800
*     SOME GENERAL STUFF: EXECUTE COMMANDS (MVC,...), ETC. TO BE USED   00008900
* --------------------------------------------------------------------- 00009000
         MNOTE '*     //////////////////////////////////////////////'   00009100
         MNOTE '*     RESERVED REGISTERS,WHICH SHOULD NOT BE CHANGED'   00009200
         MNOTE '*       &PGM1   PROGRAM REGISTER 1'                     00009300
         AIF   ('&PGM2' EQ '').NOPG2                                    00009400
         MNOTE '*       &PGM2   PROGRAM REGISTER 2'                     00009500
.NOPG2   ANOP                                                           00009600
         MNOTE '*       &WRK1   DATA/WORK AREA REGISTER 1'              00009700
         AIF   ('&WRK2' EQ '').NOWK2                                    00009800
         MNOTE '*       &WRK2   DATA/WORK AREA REGISTER 2'              00009900
.NOWK2   ANOP                                                           00010000
         MNOTE '*       RD      ACTIVE SAVE AREA CHAIN'                 00010100
         MNOTE '*     TRANSIENT REGISTERS USED/CHANGED IN MACROS'       00010200
         MNOTE '*       R0,R1,R2,RE,RF'                                 00010300
         MNOTE '*     NON TRANSIENT REGISTERS CAN BE USED IN PROGRAM'   00010400
         MNOTE '*     (IF NOT USED AS SECONDARY PROGRAM/WORK REGISTER)' 00010500
         MNOTE '*       R3,R4,R5,R6,R7,R8,R9,RA'                        00010600
         MNOTE '*     //////////////////////////////////////////////'   00010700
.NOWA    ANOP                                                           00010804
         MEND                                                           00010900

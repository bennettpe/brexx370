         MACRO                                                          00010000
&NAME    PROLOG &REG,&CODE,&ID,&CLEAR=YES,&REGS=YES,&FPR=F,            X00020000
               &GPR=R,&PARM=,&RENT=,&RREGS=(14,12),&SAVE=,&SP=0,       X00030000
               &TYPE=,&USE=NO,&USING=12,&LIST=                          00040000
.*                                                                      00050000
.*       GLOBAL VARIABLES DESCRIPTION                                   00060000
.*                                                                      00070000
.*       &REGSSW   BOOLEAN FLAG TO SIGNAL WHETHER REGISTER EQUATES      00080000
.*                    HAVE BEEN GENERATED IN A PREVIOUS EXPANSION       00090000
.*                                                                      00100000
.*       &REGSNM   CHARACTER STRING THAT CONTAINS THE EQUATE NAME       00110000
.*                    PREFIX FOR THE GENERAL REGISTERS (SEE &RN         00120000
.*                    VALUE DESCRIPTION BELOW)                          00130000
.*                                                                      00140000
.*       &USNGSW   BOOLEAN FLAG TO SIGNAL WHETHER THE  XXUSE  MACRO     00150000
.*                    IS TO BE USED IN-PLACE OF THE USING STATEMENT     00160000
.*                                                                      00170000
         GBLB  &REGSSW,&USNGSW                                          00180000
         GBLC  &REGSNM                                                  00190000
         LCLA  &A,&B,&C                                                 00200000
         LCLC  &I,&N,&R,&RN,&X,&CSECT                                   00210000
*  PROLOG  MACRO -- ASSEMBLY PROLOG  01/14/78 MARK GOTO                 00220000
         AIF   ('&LIST' EQ 'YES').COMNT                                 00230000
*          (CODE LIST=YES FOR FURTHER DESCRIPTION)                      00240000
*                                                                       00250000
         AGO   .NOCOMNT                                                 00260000
.COMNT   ANOP                                                           00270000
*    &REG,&CODE,&ID  CONSULT OPERANDS DESCRIPTION OF OS/VS2 SAVE MACRO  00280000
*                    (SUPERVISOR SERVICES AND MACRO INSTR. GC28-0683)   00290000
*                                                                       00300000
*        *NOTE* = SPECIFYING &CODE=T INSURES THAT THE CONTENTS OF       00310000
*                 R14 AND R15 ARE SAVED                                 00320000
*        *NOTE* = IF THE LENGTH OF &ID IS > 8, THEN CURRENT DATE        00330000
*                 AND TIME WILL BE APPENDED TO THE ID STRING            00340000
*        *NOTE* = IF &RENT= PARAMETER IS SPECIFIED, THEN THE &REG       00350000
*                 AND &CODE PARAMETERS ARE IGNORED                      00360000
*                                                                       00370000
*                                                                       00380000
*    ,&CLEAR=  YES/NO OPTION TO GENERATE CODE TO ZERO GETMAINED AREA    00390000
*                                                                       00400000
*        OPERATION = GENERATE CODE TO ZERO GETMAINED WORK AREA:         00410000
*                                                                       00420000
*          ORG *-2                          RE-ORIGIN OVER SVC          00430000
*          LR  3,0                          SAVE DSECT LENGTH           00440000
*          SVC 10                           ISSUE GETMAIN SVC           00450000
*          LR  2,1                          COPY DSECT AREA ADDR        00460000
*          SLR 14,14                        ZERO SOURCE ADDR            00470000
*          LR  15,14                        ZERO SOURCE LENGTH          00480000
*          MVCL 2,14                        ZERO DSECT AREA             00490000
*                                                                       00500000
*                                                                       00510000
*    ,&REGS=   YES/NO OPTION TO GENERATE REGISTER REGISTER EQUATES      00520000
*                                                                       00530000
*        OPERATION = GENERATE/SUPPRESS REGISTER EQUATES                 00540000
*                                                                       00550000
*                                                                       00560000
*    ,&FPR=    PREFIX FOR GENERATING FLOATING POINT REGISTER EQUATES    00570000
*                                                                       00580000
*        OPERATION = GENERATE REGISTER EQUATES FOR 0,2,4,6              00590000
*                                                                       00600000
*                                                                       00610000
*    ,&GPR=    PREFIX FOR GENERATING GENERAL REGISTER EQUATES           00620000
*                                                                       00630000
*        OPERATION = GENERATE REGISTER EQUATES FOR 0 THRU 15            00640000
*                                                                       00650000
*                                                                       00660000
*    ,&PARM=   PARM ADDR REG.                                           00670000
*                 OR                                                    00680000
*              (PARM ADDR REG. , LABEL)                                 00690000
*                                                                       00700000
*        OPERATION = LOAD DESIGNATED PARM REG. AND PROVIDE USING        00710000
*                    STMT. AS FOLLOWS:                                  00720000
*                                                                       00730000
*          LR  "PARM REG.",1                                            00740000
*                 OR                                                    00750000
*          LR  "PARM REG.",1                                            00760000
*          USING "LABEL","PARM REG."                                    00770000
*                                                                       00780000
*        *NOTE* = "PARM REG." MUST A DECIMAL CONSTANT OR AN             00790000
*                 EQUATE OF THE FORM  R"X"  WHERE "X" IS A              00800000
*                 DECIMAL REGISTER NUMBER                               00810000
*                                                                       00820000
*                                                                       00830000
*    ,&RENT=   DSECT ADDR REG.                                          00840000
*                 OR                                                    00850000
*              (DSECT ADDR REG. , DSECT NAME , DSECT SIZE)              00860000
*                                                                       00870000
*        OPERATION = ISSUE GETMAIN, CONNECT GETMAIN AREA AS NEW SAVE    00880000
*                    AREA, LOAD DESIGNATED DSECT ADDR REG. AND PROVIDE  00890000
*                    USING STMT. AS FOLLOWS:                            00900000
*                                                                       00910000
*          GETMAIN R,LV=72,...                                          00920000
*              .                                                        00930000
*          LR  "DSECT REG.",1                                           00940000
*                          OR                                           00950000
*          GETMAIN R,LV="DSECT SIZE",...                                00960000
*              .                                                        00970000
*          LR  "DSECT REG.",1                                           00980000
*          USING "DSECT NAME","DSECT REG."                              00990000
*                                                                       01000000
*        *NOTE* = "DSECT REG." MUST A DECIMAL CONSTANT OR AN            01010000
*                 EQUATE OF THE FORM  R"X"  WHERE "X" IS A              01020000
*                 DECIMAL REGISTER NUMBER                               01030000
*        *NOTE* = WHEN THIS PARAMETER IS SPECIFIED, &REG, &CODE AND     01040000
*                 &SAVE= PARAMETERS ARE IGNORED                         01050000
*                                                                       01060000
*                                                                       01070000
*    ,&RREGS=  (FIRST REG. , LAST REG.)                                 01080000
*                                                                       01090000
*        OPERATION = SPECIFY REGS. TO BE SAVED AS FOLLOWS:              01100000
*                                                                       01110000
*          STM "FIRST REG.","LAST REG.",12(13)                          01120000
*                                                                       01130000
*        *NOTE* = THIS PARAMETER IS IGNORED EXCEPT WHEN &RENT= IS       01140000
*                 SPECIFIED                                             01150000
*                                                                       01160000
*                                                                       01170000
*    ,&SAVE=   (TEMP REG. , SAVE AREA)                                  01180000
*                OR                                                     01190000
*    ,&SAVE=   SAVE AREA                                                01200000
*                                                                       01210000
*        OPERATION = GENERATE CODE TO CONNECT SAVE AREAS AS FOLLOWS:    01220000
*                                                                       01230000
*          LA  "TEMP REG.","SAVE AREA"                                  01240000
*          ST  13,4(,"TEMP REG.")                                       01250000
*          ST  "TEMP REG.",8(,13)                                       01260000
*          LR  13,"TEMP REG."                                           01270000
*                OR                                                     01280000
*          LR  15,13                                                    01290000
*          LA  14,"SAVE AREA"                                           01300000
*          ST  13,4(,14)                                                01310000
*          ST  14,8(,13)                                                01320000
*          LR  13,14                                                    01330000
*          LM  14,15,12(15)                                             01340000
*                                                                       01350000
*        *NOTE* = SPECIFYING THIS PARAMETER CAUSES THAT THE             01360000
*                 CONTENTS OF R14 AND R15 ARE SAVED                     01370000
*        *NOTE* = "TEMP REG." MUST A DECIMAL CONSTANT OR AN             01380000
*                 EQUATE OF THE FORM  R"X"  WHERE "X" IS A              01390000
*                 DECIMAL REGISTER NUMBER                               01400000
*        *NOTE* = IF &RENT= IS SPECIFIED, THEN THIS PARAMETER IS        01410000
*                 IGNORED                                               01420000
*                                                                       01430000
*                                                                       01440000
*    ,&SP=     SUB-POOL NUMBER FOR RE-ENTRANT GETMAIN                   01450000
*                                                                       01460000
*        OPERATION = SPECIFY THE GETMAIN SUB-POOL OPERAND:              01470000
*                                                                       01480000
*          GETMAIN R,LV=...,SP=...                                      01490000
*                                                                       01500000
*        *NOTE* = THIS PARAMETER DEFAULTS TO "SP=0" (IE. SUB-POOL       01510000
*                 NUMBER ZERO)                                          01520000
*                                                                       01530000
*                                                                       01540000
*    ,&TYPE=   'CSECT'   OR   ALIGNMENT SPECIFICATION                   01550000
*                                                                       01560000
*        OPERATION = GENERATE 'CSECT', OR 'DS' ALIGNMENT STATEMENT      01570000
*                    WITH &NAME (IF &NAME IS OMITTED, THEN &SYSECT)     01580000
*                    PARAMETER IN THE NAME FIELD.  EXAMPLE:             01590000
*                                                                       01600000
*                    "&NAME" CSECT                                      01610000
*                                      OR                               01620000
*                    "&NAME" DS  "ANY VALID OPERAND"                    01630000
*                                                                       01640000
*                                                                       01650000
*    ,&USE=    YES/NO OPTION TO GENERATE  XXUSE  MACROS IN PLACE OF     01660000
*              USING STATEMENTS                                         01670000
*                                                                       01680000
*        OPERATION = GENERATE  XXUSE  MACROS -- USING STATEMENTS WITH   01690000
*                    MEMORY (THE COMPLEMENTARY MACRO IS  XXDRP ;        01700000
*                    WHICH MAY BE USED WITH NO OPERAND TO DROP ALL      01710000
*                    ACTIVE BASE REGISTERS)                             01720000
*                                                                       01730000
*                                                                       01740000
*    ,&USING=  BASE REG.   OR   ,&USING=  (BASE REG. , LABEL)           01750000
*                                                                       01760000
*        OPERATION = GENERATE THE FOLLOWING CODE:                       01770000
*                                                                       01780000
*          LR  "BASE REG.",15                                           01790000
*          USING "&NAME / &SYSECT","BASE REG."                          01800000
*                          OR                                           01810000
*          LR  "BASE REG.",15                                           01820000
*          USING "LABEL","BASE REG."                                    01830000
*                                                                       01840000
*        *NOTE* = "BASE REG." MUST A DECIMAL CONSTANT OR AN             01850000
*                 EQUATE OF THE FORM  R"X"  WHERE "X" IS A              01860000
*                 DECIMAL REGISTER NUMBER                               01870000
*                                                                       01880000
*                                                                       01890000
.NOCOMNT AIF   ('&REGS' EQ 'NO').REGSSW                                 01900000
         AIF   (&REGSSW OR '&REGS' NE 'YES').NOREGS                     01910000
*********************************************************************** 01920000
*                                                                     * 01930000
*        REGISTER EQUATES                                             * 01940000
*                                                                     * 01950000
*********************************************************************** 01960000
&GPR.0   EQU   0                                                        01970000
&GPR.1   EQU   1                                                        01980000
&GPR.2   EQU   2                                                        01990000
&GPR.3   EQU   3                                                        02000000
&GPR.4   EQU   4                                                        02010000
&GPR.5   EQU   5                                                        02020000
&GPR.6   EQU   6                                                        02030000
&GPR.7   EQU   7                                                        02040000
&GPR.8   EQU   8                                                        02050000
&GPR.9   EQU   9                                                        02060000
&GPR.10  EQU   10                                                       02070000
&GPR.11  EQU   11                                                       02080000
&GPR.12  EQU   12                                                       02090000
&GPR.13  EQU   13                                                       02100000
&GPR.14  EQU   14                                                       02110000
&GPR.15  EQU   15                                                       02120000
&FPR.0   EQU   0                                                        02130000
&FPR.2   EQU   2                                                        02140000
&FPR.4   EQU   4                                                        02150000
&FPR.6   EQU   6                                                        02160000
         SPACE 1                                                        02170000
.REGSSW  ANOP                                                           02180000
&REGSNM  SETC  '&GPR'                      .SAVE GPR PREFIX             02190000
&REGSSW  SETB  1                           .SET FLAG TO AVOID           02200000
         AGO   .START                      . DUPLICATES IF RE-INVOKED   02210000
.NOREGS  MNOTE *,'REGS SUPRESSED'                                       02220000
.START   ANOP                                                           02230000
&RN      SETC  '&REGSNM'                                                02240000
&USNGSW  SETB  (&USNGSW OR '&USE' EQ 'YES')                             02250000
&N       SETC  '&NAME'                     .SAVE LABEL NAME ON MACRO    02260000
&CSECT   SETC  '&SYSECT'                   .SAVE CSECT NAME             02270000
         AIF   ('&TYPE' EQ '').NOTYPE      .SKIP IF TYPE OMITTED        02280000
&N       SETC  ''                          .SET NAME FIELD TO NULL      02290000
         AIF   ('&TYPE' NE 'CSECT').DSTYPE .MUST BE DS ALIGNMENT OPRND  02300000
&CSECT   SETC  '&NAME'                     .SET CSECT NAME              02310000
&NAME    CSECT                                                          02320000
         AGO   .NOTYPE                                                  02330000
.DSTYPE  ANOP                                                           02340000
&NAME    DS    &TYPE                                                    02350000
.NOTYPE  AIF   ('&ID' EQ '').NULLID        .SKIP IF ID OMITTED          02360000
&I       SETC  '&ID'                                                    02370000
         AIF   ('&ID' EQ '*').NAMEID       .IF ID REQUESTED, GO BUILD   02380000
         AIF   (K'&I LE 8).SPLITID         .IF SIMPLE PGM NAME, GO USE  02390000
         AGO   .EXPANID                    .OTHERWISE, GO EXPAND        02400000
.NAMEID  AIF   ('&NAME' EQ '').CSECTID     .IF NO NAME, TRY CSECT ID    02410000
&I       SETC  '&NAME'                                                  02420000
         AGO   .EXPANID                    .GO EXPAND NAME ID           02430000
.CSECTID AIF   ('&CSECT' EQ '').E4         .IF NO CSECT NAME, ERROR     02440000
&I       SETC  '&CSECT'                                                 02450000
.EXPANID ANOP                              .APPEND ID INFORMATION       02460000
&I       SETC  '&I_&SYSDATE_'.'&SYSTIME'(1,2).':'.'&SYSTIME'(4,2)       02470000
.SPLITID ANOP                                                           02480000
&A       SETA  ((K'&I+2)/2)*2+4            .COMPUTE BRANCH LENGTH       02490000
&N       B     &A.(0,15)                    BRANCH AROUND ID            02500000
&A       SETA  K'&I                        .SET IDENTIFIER LENGTH       02510000
         DC    AL1(&A)                      LENGTH OF IDENTIFIER        02520000
.SPLITLP AIF   (&A GT 24).SPLITUP                                       02530000
&X       SETC  '&I'(&B+1,&A)               .ISOLATE REMAINDER OF ID     02540000
         DC    CL&A'&X'                          IDENTIFIER             02550000
         AGO   .REGS                                                    02560000
.SPLITUP ANOP                                                           02570000
&X       SETC  '&I'(&B+1,24)               .SUBSTRING IDENTIFIER        02580000
         DC    CL24'&X'                          IDENTIFIER             02590000
&B       SETA  &B+24                       .INCREMENT POSITION          02600000
&A       SETA  &A-24                       .DECREMENT LENGTH            02610000
         AGO   .SPLITLP                    .GO SPLIT ID FURTHER         02620000
.NULLID  ANOP                                                           02630000
&N       DS    0H                                                       02640000
.REGS    AIF   ('&RENT' EQ '').REGSB       .SKIP IF NOT RE-ENTRANT      02650000
&A       SETA  &RREGS(1)                   .SET FIRST RENT REG. NO.     02660000
         AIF   (&A EQ 14).REGSA            .SKIP IF VALID               02670000
         MNOTE *,'RENT. REG(1) SET TO 14'                               02680000
&A       SETA  14                          .CORRECT ERROR               02690000
.REGSA   ANOP                                                           02700000
&B       SETA  &RREGS(2)                   .SET LAST RENT REG. NO.      02710000
         AIF   (&B GE 3 AND &B LE 12).REGSI     .SKIP IF VALID          02720000
         MNOTE *,'RENT. REG(2) SET TO 3'                                02730000
&B       SETA  3                           .CORRECT ERROR               02740000
         AGO   .REGSI                                                   02750000
.REGSB   AIF   ('&REG' EQ '').E1           .ERROR IF REGS OMITTED       02760000
         AIF   ('&CODE' NE '' AND '&CODE' NE 'T').E2 .INVALID CODE      02770000
&R       SETC  '&REG(1)'                   .GET REG. EXPRESSION         02780000
         AIF   (T'&REG(1) EQ 'N').REGSC    .SKIP IF VALID REG. NO.      02790000
         AIF   ('&R'(1,K'&RN) NE '&RN').E3 .SKIP IF NOT REG. EQU        02800000
&R       SETC  '&R'(K'&RN+1,K'&R-K'&RN)    .GET REG. NO.                02810000
         AIF   ('&R' LT '0' OR '&R' GT '15').E3 .ERROR IF INVALID       02820000
.REGSC   ANOP                                                           02830000
&A       SETA  &R                          .SET FIRST SAVE REG. NO.     02840000
         AIF   (&A LT 0 OR &A GT 15).E3    .ERROR IF BAD REG. NO.       02850000
&B       SETA  &A                          .ASSUME LAST = FIRST         02860000
         AIF   (N'&REG EQ 1).REGSF         .SKIP IF ONLY ONE REG.       02870000
&R       SETC  '&REG(2)'                   .GET REG. EXPRESSION         02880000
         AIF   (T'&REG(2) EQ 'N').REGSE    .SKIP IF VALID REG. NO.      02890000
         AIF   ('&R'(1,K'&RN) NE '&RN').E3 .ERROR IF NOT REG. EQU       02900000
&R       SETC  '&R'(K'&RN+1,K'&R-K'&RN)    .GET REG. NO.                02910000
         AIF   ('&R' LT '0' OR '&R' GT '15').E3 .ERROR IF INVALID       02920000
.REGSE   ANOP                                                           02930000
&B       SETA  &R                          .SET LAST SAVE REG. NO.      02940000
         AIF   (&B LT 0 OR &B GT 15).E3    .ERROR IF BAD REG. NO.       02950000
.REGSF   AIF   ('&SAVE' NE '').REGSG       .TREAT AS CODE 'T' IF SAVE   02960000
         AIF   ('&CODE' NE 'T').REGSI      .SKIP IF NOT CODE 'T'        02970000
.REGSG   AIF   (&A GE 14 OR &A LE 2).REGSH .SKIP IF R14 THRU R2         02980000
         STM   14,15,12(13)                 SAVE LINKAGE REGS           02990000
         AGO   .REGSI                                                   03000000
.REGSH   ANOP                                                           03010000
&A       SETA  14                          .SET FIRST SAVE REG. NO.     03020000
         AIF   (&B NE 14).REGSI            .INSURE THAT R15 IS SAVED    03030000
&B       SETA  15                          .SET LAST SAVE REG. NO.      03040000
.REGSI   ANOP                                                           03050000
&C       SETA  &A*4+20                     .COMPUTE DISPLACEMENT        03060000
         AIF   (&C LE 75).REGSJ            .SKIP IF NOT TOO LARGE       03070000
&C       SETA  &C-64                       .READJUST                    03080000
.REGSJ   AIF   (&A EQ &B).REGSK            .SKIP IF SAME REG. NO.       03090000
         STM   &A,&B,&C.(13)                SAVE REGISTERS              03100000
         AGO   .USING                                                   03110000
.REGSK   ST    &A,&C.(,13)                  SAVE A REGISTER             03120000
.USING   AIF   ('&USING' EQ '').RENT       .ERROR IF OMITTED            03130000
&R       SETC  '&USING(1)'                 .GET REG. EXPRESSION         03140000
         AIF   ('&R' EQ '').USINGA         .SKIP IF NULL REG. NO.       03150000
         AIF   (T'&USING(1) EQ 'N').USINGB .SKIP IF VALID REG. NO.      03160000
         AIF   ('&R'(1,K'&RN) NE '&RN').USINGA       .ERROR IF NOT EQU  03170000
&R       SETC  '&R'(K'&RN+1,K'&R-K'&RN)    .GET REG. NO.                03180000
         AIF   ('&R' GE '0' AND '&R' LE '15').USINGB .SKIP IF VALID     03190000
.USINGA  ANOP                                                           03200000
&R       SETC  '15'                        .SET DEFAULT REG.            03210000
.USINGB  AIF   (N'&USING EQ 1).USINGC      .SKIP IF NO SECOND OPERAND   03220000
&N       SETC  '&USING(2)'                 .GET ADDRESSIBILITY NAME     03230000
         AIF   ('&N' EQ '*').USINGH        .SKIP IF USING *,R           03240000
         AIF   ('&N' NE '').USINGE         .SKIP IF NON-BLANK LABEL     03250000
.USINGC  AIF   ('&NAME' EQ '').USINGD      .SKIP IF NO LABEL ON MACRO   03260000
&N       SETC  '&NAME'                     .SET ADDRESSIBILITY NAME     03270000
         AGO   .USINGE                     .GO GENERATE USING           03280000
.USINGD  AIF   ('&CSECT' EQ '').USINGH     .SKIP IF NO CSECT NAME       03290000
&N       SETC  '&CSECT'                    .SET ADDRESSIBILITY NAME     03300000
.USINGE  AIF   ('&R' EQ '15').USINGF       .SKIP IF DEFAULT REG.        03310000
         LR    &R,15                        LOAD BASE REGISTER          03320000
.USINGF  AIF   (&USNGSW).USINGG            .GENERATE SPECIAL USING      03330000
         USING &N,&RN.&R                    DEFINE ADDRESSIBILITY       03340000
         AGO   .RENT                                                    03350000
.USINGG  ANOP                                                           03360000
         XXUSE &N,&RN.&R                    DEFINE ADDRESSIBILITY       03370000
         AGO   .RENT                                                    03380000
.USINGH  ANOP                                                           03390000
         BALR  &R,0                         GET BASE ADDRESS            03400000
         AIF   (&USNGSW).USINGI            .GENERATE SPECIAL USING      03410000
         USING *,&RN.&R                     DEFINE ADDRESSIBILITY       03420000
         AGO   .RENT                                                    03430000
.USINGI  ANOP                                                           03440000
         XXUSE *,&RN.&R                     DEFINE ADDRESSIBILITY       03450000
.RENT    AIF   ('&RENT' EQ '').SAVE        .SKIP IF NOT RE-ENTRANT      03460000
&R       SETC  '&RENT(1)'                  .GET REG. EXPRESSION         03470000
&N       SETC  ''                          .SET NULL DSECT NAME         03480000
&X       SETC  '72'                        .SET DEFAULT DSECT SIZE      03490000
&A       SETA  0                           .INDICATE NO LENGTH FOUND    03500000
         AIF   ('&R' EQ '').RENTB          .SKIP IF NULL REG. NO.       03510000
.*REPLCD AIF   (T'&RENT(1) EQ 'N').RENTC   .SKIP IF VALID REG. NO.      03520000
         AIF   (T'&R EQ 'N').RENTC         .SKIP IF VALID REG. NO.      03530000
         AIF   ('&R'(1,1) NE '(').RENTA    .SKIP IF NOT PARENTHESIS     03540000
         AIF   (N'&RENT NE 2).RENTB        .SKIP IF WRONG NO. OF OPRNDS 03550000
&R       SETC  '&R'(2,K'&R-2)              .GET REG. NO.                03560000
&X       SETC  '&RENT(2)'                                               03570000
&A       SETA  2                           .SET POS. WHERE LEN. FOUND   03580000
         AGO   .GETMAIN                                                 03590000
.RENTA   AIF   ('&R'(1,K'&RN) NE '&RN').RENTB   .SKIP IF NOT REG. EQU   03600000
&R       SETC  '&R'(K'&RN+1,K'&R-K'&RN)    .GET REG. NO.                03610000
         AIF   ('&R' GE '0' AND '&R' LE '15').RENTC  .SKIP IF VALID     03620000
.RENTB   ANOP                                                           03630000
&R       SETC  '13'                        .SET DEFAULT DSECT REG.      03640000
.RENTC   AIF   (N'&RENT LT 2).GETMAIN      .SKIP IF NO DESCT NAME       03650000
&N       SETC  '&RENT(2)'                  .SAVE DSECT NAME             03660000
.RENTD   AIF   (N'&RENT LT 3).GETMAIN      .SKIP IF NO DSECT SIZE       03670000
&X       SETC  '&RENT(3)'                  .SET DSECT LENGTH            03680000
&A       SETA  3                           .SET POS. WHERE LEN. FOUND   03690000
.GETMAIN GETMAIN R,LV=&X,SP=&SP                                         03700000
         AIF   ('&CLEAR' NE 'YES').CONNECT .SKIP IF NO CLEAR            03710000
         AIF   ('&X' EQ '72' OR &A EQ 0).XC     .GO USE XC              03720000
         AIF   (T'&RENT(&A) NE 'N').MVCL   .GO USE MVCL                 03730000
         AIF   (&RENT(&A) LE 256).MVCL     .GO USE MVCL                 03740000
.XC      XC    0(&X,1),0(1)                 ZERO SAVE AREA              03750000
         AGO   .CONNECT                    .GO CONNECT SAVE AREAS       03760000
.MVCL    ORG   *-2                          RE-ORIGIN OVER SVC          03770000
         LR    3,0                          SAVE DSECT LENGTH           03780000
         SVC   10                           ISSUE GETMAIN SVC           03790000
         LR    2,1                          COPY DSECT AREA ADDR        03800000
         SLR   14,14                        ZERO SOURCE ADDR            03810000
         LR    15,14                        ZERO COUNT                  03820000
         MVCL  2,14                         ZERO DSECT AREA             03830000
.CONNECT ST    13,4(,1)                     SET OLD SAVE AREA ADDR      03840000
         ST    1,8(,13)                     SET NEW SAVE AREA ADDR      03850000
         LR    2,13                         COPY OLD SAVE AREA ADDR     03860000
         LR    13,1                         RESET SAVE AREA PTR         03870000
         LM    14,3,12(2)                   RESTORE DESTROYED REGS.     03880000
         AIF   ('&R' EQ '13').RENTE        .SKIP IF DEFAULT REG.        03890000
         LR    &R,13                        SET UP DSECT BASE           03900000
.RENTE   AIF   ('&N' EQ '').PARM           .SKIP IF NO DSECT NAME       03910000
         AIF   (&USNGSW).RENTF             .GENERATE SPECIAL USING      03920000
         USING &N,&RN.&R                    DEFINE ADDRESSIBILITY       03930000
         AGO   .PARM                                                    03940000
.RENTF   ANOP                                                           03950000
         XXUSE &N,&RN.&R                    DEFINE ADDRESSIBILITY       03960000
         AGO   .PARM                                                    03970000
.SAVE    AIF   ('&SAVE' EQ '').PARM        .SKIP IF SAVE AREA OMITTED   03980000
&X       SETC  ''                          .SET FLAG FOR SAVE RESTORE   03990000
&A       SETA  1                           .SET POS. OF SAVE LABEL      04000000
         AIF   (N'&SAVE LT 2).SAVEA        .SKIP IF REG. OMITTED        04010000
&R       SETC  '&SAVE(1)'                  .GET REG. EXPRESSION         04020000
&A       SETA  2                           .SET POS. OF SAVE LABEL      04030000
         AIF   ('&R' EQ '').SAVEA          .SKIP IF NULL REG. NO.       04040000
         AIF   (T'&SAVE(1) EQ 'N').SAVEB   .SKIP IF VALID REG. NO.      04050000
         AIF   ('&R'(1,K'&RN) NE '&RN').SAVEA   .SKIP IF NOT EQU        04060000
&R       SETC  '&R'(K'&RN+1,K'&R-K'&RN)    .GET REG. NO.                04070000
         AIF   ('&R' GE '0' AND '&R' LE '15').SAVEB  .SKIP IF VALID     04080000
.SAVEA   ANOP                                                           04090000
&R       SETC  '14'                                                     04100000
         LR    15,13                        SAVE OLD SAVE AREA ADDR     04110000
&X       SETC  '15'                        .INDICATE REGS. RESTORE      04120000
.SAVEB   ANOP                                                           04130000
&N       SETC  '&SAVE(&A)'                 .GET SAVE AREA LABEL         04140000
         LA    &R,&N                        GET NEW SAVE AREA ADDR      04150000
         ST    13,4(,&R)                    SET OLD SAVE AREA ADDR      04160000
         ST    &R,8(,13)                    SET NEW SAVE AREA ADDR      04170000
         LR    13,&R                        RESET SAVE AREA PTR         04180000
         AIF   ('&X' EQ '').PARM           .SKIP REGS. RESTORE          04190000
         LM    &R,&X,12(&X)                 RESTORE DESTROYED REGISTERS 04200000
.PARM    AIF   ('&PARM' EQ '').MEND        .SKIP IF PARM OMITTED        04210000
&R       SETC  '&PARM(1)'                  .GET REG. EXPRESSION         04220000
&N       SETC  ''                          .SET DEFAULT PARM NAME       04230000
         AIF   ('&R' EQ '').PARMA          .SKIP IF NULL REG. NO.       04240000
         AIF   (T'&PARM(1) EQ 'N').PARMB   .SKIP IF VALID REG. NO.      04250000
         AIF   ('&R'(1,K'&RN) NE '&RN').PARMA   .SKIP IF NOT EQU        04260000
&R       SETC  '&R'(K'&RN+1,K'&R-K'&RN)    .GET REG. NO.                04270000
         AIF   ('&R' GE '0' AND '&R' LE '15').PARMB  .SKIP IF VALID     04280000
.PARMA   ANOP                                                           04290000
&R       SETC  '1'                         .SET DEFAULT REG.            04300000
.PARMB   AIF   (N'&PARM EQ 1).PARMC        .SKIP IF NO SECOND OPERAND   04310000
&N       SETC  '&PARM(2)'                  .GET PARM DSECT NAME         04320000
.PARMC   AIF   ('&R' EQ '1').PARMD         .SKIP IF DEFAULT REG.        04330000
         LR    &R,1                         LOAD PARM REGISTER          04340000
.PARMD   AIF   ('&N' EQ '').MEND           .SKIP IF NO PARM DSECT NAME  04350000
         AIF   (&USNGSW).PARME             .GENERATE SPECIAL USING      04360000
         USING &N,&RN.&R                    DEFINE ADDRESSIBILITY       04370000
         AGO   .MEND                                                    04380000
.PARME   ANOP                                                           04390000
         XXUSE &N,&RN.&R                    DEFINE ADDRESSIBILITY       04400000
.MEND    MEXIT                                                          04410000
.E1      IHBERMAC 18,360                   .REG PARAMETER MISSING       04420000
         MEXIT                                                          04430000
.E2      IHBERMAC 37,360,&CODE             .INVALID CODE SPECIFIED      04440000
         MEXIT                                                          04450000
.E3      IHBERMAC 36,360,&REG              .INVALID REGS. SPECIFIED     04460000
         MEXIT                                                          04470000
.E4      IHBERMAC 78,360                   .CSECT NAME NULL             04480000
         AGO   .REGS                                                    04490000
         MEND                                                           04500000

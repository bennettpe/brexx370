         MACRO                                                          01515000
&NAME    VTCFREE  &UNALC,&DSN=,&DDN=,&MEMBER=,&DISP=,&SYSOUT=,         X01516000
               &ERROR=,&MF=AUTO,&PREFIX=,&FILE=,&F=,&DA=,&HOLD=         01517000
.*     MODIFICATIONS                                                    01517100
.*   IDENTIFIER   DATE   PERSON   REASON                                01517200
.*      001G     050284  GATES    RENAME FREE TO VTCFREE,               01517301
.*                                 RENAME DYNSPACE TO VTCDYNSP TO MAKE  01517401
.*                                 VTOC MORE TRANSPORTABLE.             01517500
         GBLA  &RCPDYN            COUNTER FOR NO ENTRIES TO MACRO       01518000
         GBLA  &DTUO              OFFSET TO TEXT UNITS                  01519000
         GBLA  &DTUPO             OFFSET TO TEXT UNIT POINTERS          01520000
         GBLB  &RCPS99(2)         TELL RCPDSECT NEED DSECTS             01521000
         GBLC  &DYNP              PREFIX FOR LABELS FOR THIS CALL       01522000
         GBLC  &DYNSP         NAME FOR AUTOMATIC STORAGE ALLOC          01523000
         LCLB  &DSECT             DSECT NEEDED FOR STORAGE, MF=E        01524000
         LCLC  &C,&T,&PAR                                               01525000
&RCPS99(1)     SETB           1                                         01526000
&RCPDYN  SETA  &RCPDYN+1          INCEREMENT COUNTER                    01527000
&DYNP    SETC  'DYN&RCPDYN' SET DEFAULT PREFIX                          01528000
&NAME    DS    0H                                                       01529000
         AIF   ('&PREFIX' EQ '').TMF                                    01530000
         AIF   (K'&PREFIX LT 4).POK                                     01531000
         MNOTE 4,'PREFIX TOO LONG, 1ST 4 CHARS USED'                    01532000
&DYNP    SETC  '&PREFIX'(1,4)                                           01533000
         AGO   .TMF                                                     01534000
.POK     ANOP                                                           01535000
&DYNP    SETC  '&PREFIX'                                                01536000
.TMF     AIF   ('&MF(1)' EQ 'G').GEN                                    01537000
         AIF   ('&MF' NE 'AUTO').TMFE                                   01538000
NAME     VTCDYNSP             GET NAME FOR SPACE                   001G 01539001
         LA    R1,&DYNSP               LOAD ADDRESS OF PARAM LIST       01540000
         USING &DYNP.DS,R1             USE GENERATED DSECT              01541000
&T       SETC  'A'                                                      01542000
&PAR     SETC  '&DYNSP+4'                                               01543000
&DSECT   SETB  1                                                        01544000
         AGO   .START                                                   01545000
.TMFE    AIF   ('&MF(2)' NE '').E2OK                                    01546000
         MNOTE 4,'PLIST ADDRESS OMITTED, MF=G USED'                     01547000
         AGO   .GEN                                                     01548000
.E2OK    ANOP                                                           01549000
&DSECT   SETB  1                                                        01550000
         AIF   ('&MF(2)' EQ '(').RMFE                                   01551000
         LA    R1,&MF(2)               LOAD PARAM LIST ADDRESS          01552000
&T       SETC  'A'                                                      01553000
&PAR     SETC  '&MF(2)+4'                                               01554000
         USING &DYNP.DS,R1             USE GENERATED DSECT              01555000
         AGO   .START                                                   01556000
.RMFE    AIF   ('&MF(2)' EQ '(R1)' OR '&MF(2)' EQ '(1)').START          01557000
&PAR     SETC  '&MF(2)'(2,K'&MF(2)-2)                                   01558000
&T       SETC  'R'                                                      01559000
         LR    R1,&PAR                 LOAD S99 PARAM LIST ADDRESS      01560000
&PAR     SETC  '4&MF(2)'                                                01561000
         USING &DYNP.DS,R1             USE GENERATED DSECT              01562000
         AGO   .START                                                   01563000
.GEN     LA    R1,&DYNP.RBP            LOAD ADDRESS OF S99 RBP          01564000
&T       SETC  'A'                                                      01565000
&PAR     SETC  '&DYNP.RB'                                               01566000
.START   LA    R15,&DYNP.RB            LOAD ADDRESS OF S99 RB           01567000
         USING S99RB,R15                                                01568000
         ST    R15,0(R1)               AND STORE IN RB POINTER          01569000
         XC    4(&DYNP.LEN-4,R1),4(R1) ZERO PARAMETER LIST              01570000
         MVI   S99RBLN,20              MOVE IN LIST LENGTH              01571000
         MVI   S99VERB,S99VRBUN        MOVE IN VERB CODE                01572000
         LA    R14,&DYNP.TUP           LOAD ADDRESS OF TU POINTERS      01573000
         ST    R14,S99TXTPP            STORE ADDRESS IN S99 RB          01574000
         LA    R15,&DYNP.TU            POINT TO SPACE FOR TEXT UNITS    01575000
         USING S99TUNIT,R15                                             01576000
&DTUO    SETA  0                                                        01577000
&DTUPO   SETA  0                                                        01578000
         AIF   ('&DSN&DA' NE '').DSN                                    01579000
         AIF   ('&SYSOUT' NE '').SYSOUT                                 01580000
.TDDN    AIF   ('&DDN&FILE&F' NE '').DDN                                01581000
.TDISP   AIF   ('&DISP' NE '').DISP                                     01582000
.TUNALC  AIF   ('&UNALC' NE '').PERM                                    01583000
.THOLD   AIF   ('&HOLD' NE '').HOLD                                     01584000
         AGO   .SVC99                                                   01585000
.DSN     RCPFDSN &DSN&DA,&MEMBER                                        01586000
         AGO   .TDDN                                                    01587000
.SYSOUT  RCPFSYS &SYSOUT                                                01588000
         AGO   .TDDN                                                    01589000
.DDN     RCPFDDN &DDN&F&FILE                                            01590000
         AGO   .TDISP                                                   01591000
.DISP RCPFDISP &DISP                                                    01592000
         AGO   .TUNALC                                                  01593000
.PERM    RCPUNALC                                                       01594000
         AGO   .THOLD                                                   01595000
.HOLD    RCPFHOLD &HOLD                                                 01596000
.SVC99   ANOP                                                           01597000
&DTUPO   SETA  &DTUPO-4                                                 01598000
         SPACE                                                          01599000
         MVI   &DYNP.TUP+&DTUPO,X'80'  SET HIGH ORDER BIT ON TEXT PTRS  01600000
         MVI   &DYNP.RBP,X'80'         SET HIGH ORDER BIT ON RB PTR     01601000
         RCPSR2 UNSAVE                                                  01602000
&DTUPO   SETA  &DTUPO+4                                                 01603000
         AIF   (NOT &DSECT).DYNA                                        01604000
         DROP  R1,R15                  DEACTIVATE ADDRESSABILITY        01605000
.DYNA    DYNALLOC                                                       01606000
         AIF   ('&ERROR' EQ '').RESERVE                                 01607000
         AIF   ('&PAR' EQ '').LTR                                       01608000
         L&T   R14,&PAR                 LOAD REG 14 WITH ADDRESS OF RB  01609000
         AIF   (NOT &DSECT).LTR                                         01610000
         USING &DYNP.RB,R14            SET UP ADDRESSABILITY            01611000
.LTR     LTR   R15,R15                 TEST RETURN CODE                 01612000
         BNZ   &ERROR                  BRANCH IF NON ZERO               01613000
**       NOTE.  R14 POINTS TO REQUEST BLOCK, R15 HAS RETURN CODE     ** 01614000
.RESERVE AIF   (&DSECT).RESDS                                           01615000
         SPACE                                                          01616000
*********************************************************************** 01617000
**       RESERVE SPACE FOR DYNALLOC DATA                             ** 01618000
*********************************************************************** 01619000
         RCPDS                                                          01620000
.SSP     ANOP                                                           01621000
&DYNP.RBP DS   F                       SVC 99 REQ BLOCK POINTER         01622000
&DYNP.RB  DS   5F                      SVC 99 REQUEST BLOCK             01623000
&DYNP.TUP DS   CL&DTUPO                SPACE FOR TEXT POINTERS          01624000
         AIF   (&DTUO EQ 0).DTU11                                       01625000
&DYNP.TU  DS   CL&DTUO                 SPACE FOR TEXT UNITS             01626000
         AGO   .DTU10                                                   01627000
.DTU11   ANOP                                                           01628000
&DYNP.TU  DS   0C                      NO SPACE NEEDED FOR TEXT UNITS   01629000
.DTU10   ANOP                                                           01630000
&DYNP.LEN EQU  *-&DYNP.RBP             LENGTH OF SPACE USED             01631000
         AIF   (&DSECT).DSP                                             01632000
         RCPDS                                                          01633000
         SPACE 3                                                        01634000
         AGO   .EXIT                                                    01635000
.RESDS   ANOP                                                           01636000
         AIF   ('&DYNSP' EQ '').SP3                                     01637000
         VTCDYNSP ADD                                              001G 01638001
.SP3     SPACE                                                          01639000
&DYNP.DS DSECT                         DSECT TO MAP SVC 99 DATA         01640000
         AGO   .SSP                                                     01641000
.DSP     AIF   ('&MF(3)' EQ '').END1                                    01642000
&MF(3)   EQU   &DYNP.LEN               LENGTH OF AREA                   01643000
.END1    ANOP                                                           01644000
&SYSECT  CSECT                                                          01645000
         SPACE 3                                                        01646000
.EXIT    MEND                                                           01647000
